## Set your shelf variable for when you're no longer in noob dungeon.
if skill 'magery' >= 80
	if not varexists shelf
		@setvar shelf
	endif
endif
## Main script
if skill 'magery' != 100
	if skill 'magery' < 60
		## Cast lightning
		if hp > 30
			if mana > 30
				cast 'lightning'
				while not targetexists
				endwhile
				while targetexists
					## If a mongbat is close target that instead of yourself - less downtime!
					if findtype '39' any any any 3
						targettype '39' any any any 3
					else
						hotkey 'Target self'
					endif
				endwhile
				pause 500
			else 
				##Meditation to get mana
				while mana < 30
					if not findbuff 'actively meditating'
						skill 'meditation'
					else
						pause 1000
					endif
				endwhile
			endif
		endif	
	elseif skill 'magery' < 75
		## Cast invis
		if mana > 30
			cast 'invisibility'
			while not targetexists
			endwhile
			while targetexists
				hotkey 'Target self'
			endwhile
			pause 500
		else
			while mana < 50
				if findbuff 'Actively meditating'
					pause 1000
				else
					skill 'Meditation'
				endif
			endwhile
		endif
		
	elseif skill 'magery' < 80
		## Cast meteor swarm
		if mana > 50
			cast 'meteor swarm'
			while not targetexists
			endwhile
			while targetexists
				hotkey 'Target self'
			endwhile
			pause 500
		else
			while mana < 60
				if findbuff 'Actively meditating'
					pause 1000
				else
					skill 'Meditation'
				endif
			endwhile
		endif	
	elseif skill 'magery' < 85 
		## It is advised to have a second character to heal yourself with bandages to avoid cooldown
		## Cast flamestrike on yourself
		
		## Restock regs
		if counttype '3981' backpack < 10
			@menu shelf 1
		endif
		if hp > 50
			if mana > 40
				cast 'flamestrike'
				while not targetexists
				endwhile
				while targetexists
					hotkey 'Target self'
				endwhile
				pause 500
			else
				while mana < 40 
					if not findbuff 'Actively meditating'
						skill 'meditation'
						pause 1000
					else
						pause 1000
					endif
				endwhile
			endif	
		endif
	elseif skill 'magery' < 100
		## Cast summon water elemental
		## Restock regs if low
		if counttype '3981' backpack < 5
			@menu shelf 1
		endif
		if mana > 50
			cast 'Water elemental'
			while not findtype "a water elemental" ground 0 0 1
				pause 250
			endwhile
			if findtype "a water elemental" as ele
				@menu ele 9
			endif
			pause 500
		else
			while mana < 50
				if findbuff 'Actively meditating'
					wait 1000
				else
					skill 'meditation'
					pause 1000
				endif
			endwhile
		endif
	endif
endif
replay
